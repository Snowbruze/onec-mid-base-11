
Процедура ДополнитьФорму(Форма) Экспорт
	// ++ Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 1
	Имя = Форма.ИмяФормы;
	
	Если Имя = "Документ.ЗаказПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);
	// ++ Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 2
		ДобавитьРаботуСоСкидкой(Форма);
	// -- Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 2
	ИначеЕсли Имя = "Документ.ОплатаОтПокупателя.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоПередДоговором(Форма);	
	ИначеЕсли Имя = "Документ.ОплатаПоставщику.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоПередДоговором(Форма);	
	ИначеЕсли Имя = "Документ.ПоступлениеТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);	
	ИначеЕсли Имя = "Документ.РеализацияТоваровУслуг.Форма.ФормаДокумента" Тогда
		ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма);	
	КонецЕсли;
	// -- Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 1
КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицоВГруппаШапкаПраво(Форма)
	// ++ Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 1
	ПолеКонтактноеЛицо = Форма.Элементы.Добавить("нт_КонтактноеЛицо", Тип("ПолеФормы"), Форма.Элементы.ГруппаШапкаПраво);
	ПолеКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ПолеКонтактноеЛицо.ПутьКДанным = "Объект.нт_КонтактноеЛицо";
	// --
КонецПроцедуры

Процедура ДобавитьПолеКонтактноеЛицоПередДоговором(Форма)
	// ++ Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 1
	ПолеКонтактноеЛицо = Форма.Элементы.Вставить("нт_КонтактноеЛицо", Тип("ПолеФормы"),, Форма.Элементы.Договор);
	ПолеКонтактноеЛицо.Вид = ВидПоляФормы.ПолеВвода;
	ПолеКонтактноеЛицо.ПутьКДанным = "Объект.нт_КонтактноеЛицо";
	// --
КонецПроцедуры

Процедура ДобавитьРаботуСоСкидкой(Форма)
	// ++ Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 2
	ГруппаСкидка = Форма.Элементы.Добавить("нт_ГруппаСкидка", Тип("ГруппаФормы"), Форма.Элементы.ГруппаШапкаЛево);
	ГруппаСкидка.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСкидка.ОтображатьЗаголовок = Ложь;
	
	ПолеСкидка = Форма.Элементы.Добавить("нт_СогласованнаяСкидка", Тип("ПолеФормы"), Форма.Элементы.нт_ГруппаСкидка);
	ПолеСкидка.Вид = ВидПоляФормы.ПолеВвода;
	ПолеСкидка.Заголовок = "Согласованная скидка";
	ПолеСкидка.ПутьКДанным = "Объект.нт_СогласованнаяСкидка";
	ПолеСкидка.УстановитьДействие("ПриИзменении", "нт_СогласованнаяСкидкаПриИзменении");
	ПолеСкидка.Подсказка = "%";
	ПолеСкидка.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСправа;
	
	КомандаПересчетаТаблиц = Форма.Команды.Добавить("нт_КомандаПересчитатьТаблицы");
	КомандаПересчетаТаблиц.Заголовок = "Пересчитать таблицы";
	КомандаПересчетаТаблиц.Действие = "нт_ПересчитатьТаблицы";
	
	КнопкаПересчетаТаблиц = Форма.Элементы.Добавить("нт_КнопкаПересчитатьТаблицы", Тип("КнопкаФормы"),
		Форма.Элементы.нт_ГруппаСкидка);
	КнопкаПересчетаТаблиц.Вид = ВидКнопкиФормы.ОбычнаяКнопка;
	КнопкаПересчетаТаблиц.ИмяКоманды = "нт_КомандаПересчитатьТаблицы";
	КнопкаПересчетаТаблиц.Картинка = БиблиотекаКартинок.Обновить;
	КнопкаПересчетаТаблиц.Отображение = ОтображениеКнопки.КартинкаИТекст;
	// --	
КонецПроцедуры