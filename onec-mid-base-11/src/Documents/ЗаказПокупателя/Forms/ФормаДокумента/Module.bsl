
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	// ++ Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 2
	//ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	нт_ПересчитатьСуммуСтроки(ТекущиеДанные);
	// --
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область нт_Доработки

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура нт_СогласованнаяСкидкаПриИзменении(Элемент)
	// ++ Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 2
	Если Объект.Товары.Количество() > 0 ИЛИ Объект.Услуги.Количество() > 0 Тогда 	
		нт_ЗадатьВопросОбИзмененииСуммы();
	КонецЕсли;
	// --
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура нт_ПересчитатьТаблицы(Команда)
 	// ++ Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 2
	нт_ПересчитатьТаблицу(Объект.Товары);
	нт_ПересчитатьТаблицу(Объект.Услуги);
	РассчитатьСуммуДокумента();
	// --
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура нт_ЗадатьВопросОбИзмененииСуммы()
 	// ++ Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 2
	ТекстВопроса = "Изменён процент скидки. Пересчитать таблицы товаров и услуг?";
	ОтветПользователя = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет,,, "Изменён процент");
	
	Если ОтветПользователя = КодВозвратаДиалога.Да Тогда
		нт_ПересчитатьТаблицу(Объект.Товары);
		нт_ПересчитатьТаблицу(Объект.Услуги);
		РассчитатьСуммуДокумента();
	КонецЕсли;	
	// --
КонецПроцедуры

&НаКлиенте
Процедура нт_ПересчитатьТаблицу(Таблица)
	// ++ Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 2
	Для Каждого Строка Из Таблица Цикл
		нт_ПересчитатьСуммуСтроки(Строка);
	КонецЦикла;
	// --
КонецПроцедуры

&НаКлиенте
Процедура нт_ПересчитатьСуммуСтроки(Строка)
 	// ++ Демещенко А.В. 14.02.2024 - Домашнее задание 11.2, Задача 2
	СуммаСтроки = Строка.Цена * Строка.Количество;
	Строка.Сумма = СуммаСтроки - СуммаСтроки * (Объект.нт_СогласованнаяСкидка / 100);
	// --
КонецПроцедуры

#КонецОбласти

#КонецОбласти